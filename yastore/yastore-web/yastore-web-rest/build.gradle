apply plugin: 'war'

description = "Yet Another eStore :: Web Frontend"

dependencies {
    compile (
        project (':yastore-core:yastore-commons'),
        project (':yastore-core:yastore-domain'),
        project (':yastore-core:yastore-services'),

        [group: 'ch.qos.logback', name : 'logback-classic', version : "${logbackVersion}"],
        [group: 'ch.qos.logback', name : 'logback-core', version : "${logbackVersion}"],
        [group: 'org.slf4j', name : 'jcl-over-slf4j', version : "${slf4jVersion}"],
        [group: 'org.slf4j', name : 'jul-to-slf4j', version : "${slf4jVersion}"],
        [group: 'org.slf4j', name : 'log4j-over-slf4j', version : "${slf4jVersion}"],

        [group: 'org.codehaus.jackson', name : 'jackson-xc', version : "${jacksonVersion}"],
        [group: 'org.codehaus.jackson', name : 'jackson-core-asl', version : "${jacksonVersion}"],
        [group: 'org.codehaus.jackson', name : 'jackson-jaxrs', version : "${jacksonVersion}"],
        [group: 'org.codehaus.jackson', name : 'jackson-mapper-asl', version : "${jacksonVersion}"],
        [group: 'org.codehaus.jackson', name : 'jackson-mapper-asl', version : "${jacksonVersion}"],
        [group: 'com.thoughtworks.xstream', name : 'xstream', version : "1.4.7"],
        
        // REST web services related dependencies
        [group: 'com.fasterxml.jackson.jaxrs', name : 'jackson-jaxrs-json-provider', version : "2.4.0"],
        [group: 'com.fasterxml.jackson.core', name : 'jackson-databind', version : "2.4.0"],

        [group: 'org.springframework', name : 'spring-web', version : "${springVersion}"],
        [group: 'org.springframework', name : 'spring-webmvc', version : "${springVersion}"],
        
        [group: 'org.hibernate', name : 'hibernate-validator', version : '5.1.1.Final'],

        // Command line configuration + properties files configuration
        [group: 'commons-configuration', name : 'commons-configuration', version : "${commonsConfigurationVersion}"],
        [group: 'commons-beanutils', name : 'commons-beanutils', version : '1.8.3']            
    )
	compile("org.springframework.security:spring-security-web:${springSecurityVersion}") {
        exclude group: 'org.springframework'
    }
	compile("org.springframework.security:spring-security-config:${springSecurityVersion}") {
        exclude group: 'org.springframework'
    }    
    // Spring Data REST web services
    compile("org.springframework.data:spring-data-rest-webmvc:${springRESTVersion}") {
		exclude group: 'org.springframework'
    }

    provided (
        [group: 'javax.servlet', name : 'javax.servlet-api', version : '3.0.1'],
        [group: 'javax.servlet.jsp', name : 'javax.servlet.jsp-api', version : '2.3.1'],
    )    
}

// Generate Eclipse Web Fragment facet
eclipse {
    wtp {
        facet {
            facet name: 'jst.webfragment', version: '3.0'
        }
    }
}

// -----------------------------------------------------
// Add web fragment resources to correct folder: META-INF/resources
def webResources = "$buildDir/web-resources"
task copyWebResources(type: Copy) {
    from "src/main/webapp"
    into "$webResources/META-INF/resources"
	
    includeEmptyDirs = true
}
sourceSets.main.output.dir(webResources, builtBy: 'copyWebResources')
// -----------------------------------------------------

jar {
    enabled = true
}
/*
 *  use the jar as main artifact, hence letting other projects depend upon this one
 *  the war is still available with a 'war' classifier
 */
artifacts {  archives jar }

